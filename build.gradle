plugins {
    id 'groovy'
    id 'application'
    id 'com.github.ben-manes.versions' version '0.42.0'
    // uncomment next line when using JDK11+
    //id 'com.autonomousapps.dependency-analysis' version '1.13.1'
}

ext.groovyVersion = '4.0.6'
ext.eclipseCollectVersion = '11.1.0'
ext.tablesawVersion = '0.43.1'

repositories {
    mavenCentral()
//    mavenLocal()
}

dependencies {
    implementation "org.apache.groovy:groovy:$groovyVersion"
    implementation "org.apache.groovy:groovy-swing:$groovyVersion"
    implementation "org.eclipse.collections:eclipse-collections-api:$eclipseCollectVersion"
    implementation "org.eclipse.collections:eclipse-collections:$eclipseCollectVersion"
    implementation 'org.apache.commons:commons-math3:3.6.1'
    implementation "tech.tablesaw:tablesaw-core:$tablesawVersion"
    implementation "tech.tablesaw:tablesaw-aggregate:$tablesawVersion"
    implementation 'org.plotly-scala:plotly-render_2.13:0.8.3'
    implementation('org.codehaus.gpars:gpars:1.2.1') {
        transitive false
    }
    runtimeOnly 'org.slf4j:slf4j-simple:2.0.3'
}

group = 'org.apache.groovy'
version = '1.0.0-SNAPSHOT'
description = 'Eclipse Kata Groovy Solutions'

tasks.withType(GroovyCompile).configureEach {
    options.encoding = 'UTF-8'
    groovyOptions.forkOptions.jvmArgs += ['-Dgroovy.extension.disable=groupBy,countBy']
}

application {
    mainClass = 'FruitColors'
}

tasks.named('run').configure {
    description = "Run 'FruitColors' as a JVM application/Groovy script"
    jvmArgs += ['-Dgroovy.extension.disable=groupBy,countBy']
}
